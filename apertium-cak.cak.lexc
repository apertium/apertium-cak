!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                  ! Sustantivo
%<np%>                 ! Nombre propio
%<v%>                  ! Verbo
%<vaux%>               ! Verbo auxiliar
%<prn%>                ! Pronombre
%<adj%>                ! Adjetivo
%<adv%>                ! Adverbio
%<det%>                ! Determinante
%<cnjsub%>             ! Conjunción subordinadora
%<cm%>                 ! 
%<sent%>               ! 

%<iv%>                 ! Intransitivo
%<tv%>                 ! Transitivo
 
%<inf%>                ! Infinitivo 
%<pp%>                 ! Participio pasado

%<impf%>               ! Imperfectivo
%<perf%>               ! Perfectivo
%<pot%>                ! Potencial

%<cmpl%>               ! Completivo

%<s_sg1%>              ! Concordancia de sujeto de la 1ra persona singular
%<s_sg2%>              ! Concordancia de sujeto de la 2da persona singular
%<s_sg3%>              ! Concordancia de sujeto de la 3ra persona singular
%<s_pl1%>              ! Concordancia de sujeto de la 1ra persona plural
%<s_pl2%>              ! Concordancia de sujeto de la 2da persona plural
%<s_pl3%>              ! Concordancia de sujeto de la 3ra persona plural

%<o_sg1%>              ! Concordancia de objeto de la 1ra persona singular
%<o_sg2%>              ! Concordancia de objeto de la 2da persona singular
%<o_sg3%>              ! Concordancia de objeto de la 3ra persona singular
%<o_pl1%>              ! Concordancia de objeto de la 1ra persona plural
%<o_pl2%>              ! Concordancia de objeto de la 2da persona plural
%<o_pl3%>              ! Concordancia de objeto de la 3ra persona plural

%<px1sg%>              ! Posesivo, 1a singular
%<px2sg%>              ! Posesivo, 2a singular
%<px3sg%>              ! Posesivo, 3a singular
%<px1pl%>              ! Posesivo, 1a plural
%<px2pl%>              ! Posesivo, 2a plural
%<px3pl%>              ! Posesivo, 3a plural

@P.Poss.Sg1@
@P.Poss.Sg2@
@P.Poss.Sg3@
@P.Poss.Pl1@
@P.Poss.Pl2@
@P.Poss.Pl3@

@R.Poss.Sg1@
@R.Poss.Sg2@
@R.Poss.Sg3@
@R.Poss.Pl1@
@R.Poss.Pl2@
@R.Poss.Pl3@

@D.Poss@               ! Posesivo prohibido

@P.Prefijo.1@
@R.Prefijo.1@
@D.Prefijo@

%{a%}                  ! Underlying -a- deletes before vowel
%{i%}                  ! Underlying -i- deletes before vowel
%{u%}                  ! Underlying -u- deletes before vowel
%{o%}                  ! -uj o -oj
%{n%}                  ! Para el posesivo de 1a persona, nu-,w-
%{w%}                  ! -w- epentética
%{t%}                  ! -t- en prefijos de persona
%{ъ%}                  ! -ʼ- en prefijos de persona
%{V%}                  ! Vocal de raíz
%{≤%}                  ! Marcador fonológico de sustantivos de la clase II 

ʼ
%>                     ! Frontera entre morfemas

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Misceláneas ; 
Preposiciones ; 
Determinantes ; 
Pronombres ; 
Numerales ;
Sustantivos ; 
Propios ;
Adjetivos ;
Adverbios ;
Punctuación ;

Prefijos ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prefijos

Possessives ;
Ordinal ; 
Verbal ;

LEXICON Possessives 

! _ Cns: nu-, a-, ru-, qa-, i-, ki-
! _ Vow: w- , aw-, r-, q-,  iw-, k-

@P.Poss.Sg1@:@P.Poss.Sg1@%{n%}%{u%}%> Sustantivos ;
@P.Poss.Sg2@:@P.Poss.Sg2@a%{w%}%> Sustantivos ;
@P.Poss.Sg3@:@P.Poss.Sg3@r%{u%}%> Sustantivos ;
@P.Poss.Pl1@:@P.Poss.Pl1@q%{a%}%> Sustantivos ;
@P.Poss.Pl2@:@P.Poss.Pl2@i%{w%}%> Sustantivos ;
@P.Poss.Pl3@:@P.Poss.Pl3@k%{i%}%> Sustantivos ;

LEXICON Ordinal 

@P.Poss.Sg3@:@P.Poss.Sg3@r%{u%}%> Numerales ;

LEXICON Poss

@R.Poss.Sg1@%<px1sg%>:@R.Poss.Sg1@ # ;
@R.Poss.Sg2@%<px2sg%>:@R.Poss.Sg2@ # ;
@R.Poss.Sg3@%<px3sg%>:@R.Poss.Sg3@ # ;
@R.Poss.Pl1@%<px1pl%>:@R.Poss.Pl1@ # ;
@R.Poss.Pl2@%<px2pl%>:@R.Poss.Pl2@ # ;
@R.Poss.Pl3@%<px3pl%>:@R.Poss.Pl3@ # ;

LEXICON N1 
! Class 1 nouns: No change on possession
%<n%>: Poss ;
@D.Poss@%<n%>:@D.Poss@ # ;

LEXICON N2
! Class 2 nouns: Change from lax to tense vowel 
! on possession
%<n%>:%{≤%} Poss ;
@D.Poss@%<n%>:@D.Poss@ # ;

LEXICON N3/axel

%<n%>: Poss ;
@D.Poss@%<n%>:@D.Poss@axel # ;

LEXICON N3/aj

%<n%>: Poss ;
@D.Poss@%<n%>:@D.Poss@aj # ;

LEXICON N5/iʼ

%<n%>: Poss ;
%<n%>%<pl%>:%>iʼ # ;
@D.Poss@%<n%>:@D.Poss@ # ;

LEXICON N5/aʼ

%<n%>: Poss ;
%<n%>%<pl%>:%>aʼ # ;
@D.Poss@%<n%>:@D.Poss@ # ;

LEXICON N5/bʼoniʼ

%<n%>: Poss ;
%<n%>%<pl%>:%>bʼoniʼ # ;
@D.Poss@%<n%>:@D.Poss@ # ;

LEXICON N6 
! Sustantivos relacionales

%<n%>%<rel%>: Poss ;

LEXICON SUSTANTIVO
! Class to be determined.
N1 ; 

LEXICON NP-ANT

%<np%>%<ant%>: #  ;


LEXICON Verbal 

! Pres/Past/Pot
! Impf/Perf
! Ind/Hort

@P.Prefijo.1@:@P.Prefijo.1@ Accidents ; 
                            Intr ; 
                            Tran ; 

LEXICON Accidents

Impf ; 
Perf ; 

LEXICON Impf

%<impf%>:y%> PERS-IV-A ; 
%<impf%>:n%{i%}%> PERS-IV-A-3 ; 

%<impf%>:y%> PERS-TV-A ; 
%<impf%>:n%{i%}%> PERS-TV-A-3 ; 

LEXICON Perf 

%<perf%>:x%> PERS-IV-A ;
%<perf%>:x%> PERS-IV-A-3 ;

%<perf%>:x%> PERS-TV-A ;

LEXICON Pot

%<pot%>:xk%> PERS-IV-A ;
%<pot%>:xt%> PERS-IV-A-3 ;

%<pot%>:xk%> PERS-TV-A ;

LEXICON PERS-IV-A ! Juego absolutivo

%<s_sg1%>:i%{n%}%> Intr ; ! y-in-el, y-i-wär   = -i{n}-
%<s_sg2%>:a%{t%}%> Intr ; ! y-at-el, y-a-wär   = -a{t}-
                          ! n-Ø-el , ni-Ø-wär  = -n{i}-
%<s_pl1%>:oj%> Intr ;     ! y-oj-el, y-oj-wär  = -oj-
%<s_pl2%>:ix%> Intr ;     ! y-ix-el, y-ix-wär  = -ix-
%<s_pl3%>:e%{ъ%}%> Intr ; ! y-e'-el, y-e-wär   = -e{ъ}-

LEXICON PERS-TV-A ! Juego absolutivo

%<o_sg1%>:i%{n%}%> PERS-TV-B ; ! y-in-el, y-i-wär   = -i{n}-
%<o_sg2%>:a%{t%}%> PERS-TV-B ; ! y-at-el, y-a-wär   = -a{t}-
                               ! n-Ø-el , ni-Ø-wär  = -n{i}-
%<o_pl1%>:oj%>     PERS-TV-B ; ! y-oj-el, y-oj-wär  = -oj-
%<o_pl2%>:ix%>     PERS-TV-B ; ! y-ix-el, y-ix-wär  = -ix-
%<o_pl3%>:e%{ъ%}%> PERS-TV-B ; ! y-e'-el, y-e-wär   = -e{ъ}-

LEXICON PERS-TV-B ! Juego ergativo

! _ Cns: nu-, a-, ru-, qa-, i-, ki-
! _ Vow: w- , aw-, r-, q-,  iw-, k-

%<s_sg1%>:%{n%}%{u%}%> Tran ;
%<s_sg2%>:a%{w%}%> Tran ;
%<s_sg3%>:r%{u%}%> Tran ;
%<s_pl1%>:q%{a%}%> Tran ;
%<s_pl2%>:i%{w%}%> Tran ;
%<s_pl3%>:k%{i%}%> Tran ;

LEXICON PERS-IV-A-3

%<s_sg3%>: Intr ; 

LEXICON PERS-TV-A-3

%<o_sg3%>: PERS-TV-B ; 

LEXICON TV 

%<v%>%<tv%>: # ; 
@D.Prefijo@%<v%>%<tv%>%<inf%>:@D.Prefijo@%{≤%}%>%{o%}j # ;
@R.Prefijo.1@%<v%>%<tv%>%<cmpl%>:@R.Prefijo.1@%{≤%}%>%{V%}täj # ; ! TODO: Does this only apply to passive / do we need to switch the agreement?

LEXICON IV 

%<v%>%<iv%>: # ; 
@D.Prefijo@%<v%>%<iv%>%<pp%>:@D.Prefijo@%>%{i%}näq # ;

LEXICON A 

%<adj%>: # ; 

LEXICON ADV

%<adv%>: # ; 

LEXICON NUM1234

@D.Prefijo@%<num%>:@D.Prefijo@ # ; 
@R.Poss.Sg3@%<num%>%<ord%>:@R.Poss.Sg3@ # ; 

LEXICON NUM-DIST

%<num%>%<dist%>: # ; 

LEXICON NUM5

@D.Prefijo@%<num%>:@D.Prefijo@ # ; 
@R.Poss.Sg3@%<num%>%<ord%>:@R.Poss.Sg3@ # ; 
@D.Prefijo@%<num%>%<dist%>:@D.Prefijo@%>täq # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Misceláneas

ajin%<vaux%>:ajin # ; 
ajin%<vaux%>:tajin # ; 

ta%<mod%>:ta # ;

taq%<cnjsub%>:taq # ; 
chuqaʼ%<cnjcoo%>:chuqaʼ # ; 

! kʼo = hay

ti%<dim%>:ti # ; 

LEXICON Preposiciones 

pa%<pr%>:pa # ; 
pa%<pr%>:pan # ; 
chi%<pr%>:chi # ; 

LEXICON Determinantes

ri%<det%>:ri # ;
riʼ%<det%>:riʼ # ;

re%<det%>%<dem%>:re # ;
reʼ%<det%>%<dem%>:reʼ # ;

la%<det%>%<dem%>:la # ;
laʼ%<det%>%<dem%>:laʼ # ;

LEXICON Pronombres

! Juego absolutivo
in%<prn%>%<pers%>:in # ; 
at%<prn%>%<pers%>:at # ; 
!
öj%<prn%>%<pers%>:öj # ; 
ix%<prn%>%<pers%>:ix # ; 
e%<prn%>%<pers%>:e # ; 

rïn%<prn%>%<pos%>:rïn # ;
rat%<prn%>%<pos%>:rat # ;
rijaʼ%<prn%>%<pos%>:rijaʼ # ;
röj%<prn%>%<pos%>:röj # ;
rïx%<prn%>%<pos%>:rïx # ;
rijeʼ%<prn%>%<pos%>:rijeʼ # ;

ririʼ%<prn%>%<dem%>:ririʼ # ;
rijaʼ%<prn%>%<pers%>:rijaʼ # ;

LEXICON Numerales 

jun:jun NUM1234 ;  ! "uno"
kaʼiʼ:kaʼiʼ NUM1234 ; ! ""
oxiʼ:oxiʼ NUM1234 ; ! ""
kajiʼ:kajiʼ NUM1234 ; ! ""
woʼoʼ:woʼoʼ NUM5 ; ! ""
waqiʼ:waqiʼ NUM5 ; ! "seis"
wuquʼ:wuquʼ NUM5 ; ! ""
waqxaqiʼ:waqxaqiʼ NUM5 ; ! ""
bʼelejeʼ:bʼelejeʼ NUM5 ; ! ""
lajuj:lajuj NUM5 ; ! ""
julajuj:julajuj NUM5 ; ! ""
kabʼlajuj:kabʼlajuj NUM5 ; ! ""
oxlajuj:oxlajuj NUM5 ; ! ""
wolajuj:wolajuj NUM5 ; ! ""
waqlajuj:waqlajuj NUM5 ; ! ""
wuqlajuj:wuqlajuj NUM5 ; ! ""
waqxaqlajuj:waqxaqlajuj NUM5 ; ! ""
bʼelejlajuj:bʼelejlajuj NUM5 ; ! ""
juwinäq:juwinäq NUM5 ; ! ""
jukʼal:jukʼal NUM5 ; ! ""

jun:jujun NUM-DIST ;  ! ""
kaʼiʼ:kakaʼ NUM-DIST ; ! ""
oxiʼ:oxʼox NUM-DIST ; ! ""
kajiʼ:kajkäj NUM-DIST ; ! ""

LEXICON Relacionales

ichin:ichin N6 ; ! "de"
oma:oma N6 ; ! ""
oma:uma N6 ; ! "" ! Use/Sub
ikʼin:ikʼin N6 ; ! ""
!!
ij:ij N6 ; ! ""
xeʼ:xeʼ N6 ; ! ""
kojöl:kojöl N6 ; ! ""
xkïn:xkïn N6 ; ! ""
wiʼ:wiʼ N6 ; ! ""
tzaʼm:tzaʼm N6 ; ! ""

LEXICON Sustantivos

Relacionales ;

wuj:wuj N1 ; ! "libro"
poʼt:poʼt N1 ; ! "blusa"
chʼatal:chʼatal N1 ; ! "mesa"
chachal:chachal N1 ; ! "collar"
taqkil:taqkil N1 ; ! "recado"

tzʼibʼabʼäl:tzʼibʼabʼäl N2 ; ! "boligrafo"
tinamït:tinamït N2 ; ! "ciudad"
chʼkät:chʼkät N2 ; ! "silla"
wäy:wäy N2 ; ! "tortilla"
xrëq:xrëq N2 ; ! "sapo"
xpëq:xpëq SUSTANTIVO ; ! "sapo, frog"
chakäch:chakäch N2 ; ! "cesta"
qʼejelonïk:qʼejelonïk N2 ; ! "saludo"
qʼös:qʼös N2 ; ! "pienso"
läq:läq N2 ; ! "plato"

chʼabäl:chʼabäl N1 ; ! "lengua"
äj:äj N1 ; ! "espiga"
achibʼäl:achibʼäl N1 ; ! "imagen"
kʼïx:kʼïx N1 ; ! "vergüenza"
paläj:paläj N1 ; ! "cara"

juxbʼäl:juxbʼäl N2 ; ! "rotulador"
yojbʼäl:yojbʼäl N2 ; ! "borrador"
samajibʼäl:samajibʼäl N2 ; ! "herramienta"
oyonibʼäl:oyonibʼäl N2 ; ! "teléfono"
chapbʼäl:chapbʼäl N2 ; ! "afiler"

chʼakulaj:chʼakul N3/aj ; ! "cuerpo"
chiʼaj:chiʼ N3/aj ; ! "boca"
!chiʼaj:chiʼaj SUSTANTIVO-INALIENABLE ; ! "boca, mouth"
pamaj:pam N3/aj ; ! "estómago"
jolomaj:jolom N3/aj ; ! "cabeza" ! TODO: p.64 jolon/m ?
!jolomaj:jolomaj SUSTANTIVO-INALIENABLE ; ! "cabeza, head"

nimalaxel:nimal N3/axel ; ! "hermano/a mayor del mismo género"

äk:äk N1 ; ! "gallina"
äkʼ:äkʼ SUSTANTIVO ; ! "pollo, chicken"
ikäj:ikäj N1 ; ! "hacha"
oj:oj N1 ; ! "aguacate"
chun:chun N1 ; ! "cal"
bʼey:bʼey N1 ; ! "camino"
juyuʼ:juyuʼ N1 ; ! "montaña"
kotzʼiʼj:kotzʼiʼj N1 ; ! "flor" ! TODO: p.63 kiotz'i'j
tzʼiʼ:tzʼiʼ N1 ; ! "perro"
kʼuxuʼ:kʼuxuʼ N1 ; ! "achiote"
waq:waq N1 ; ! "cerdo"
samaj:samaj N1 ; ! "trabajo"

aqanaj:aqan N3/aj ; ! "pierna" ! TODO: p.65 waqän
!aqanaj:aqanaj SUSTANTIVO-INALIENABLE ; ! "pierna, leg"


!ibʼöchʼ:ibʼöchʼ N4/il ; ! "vena"

utiw:utiw N5/aʼ ; ! ""
umül:umül N5/iʼ ; ! "conejo, rabbit"
bʼalam:bʼalam N5/iʼ ; ! "jaguar, jaguar"
tzʼikïn:tzʼikïn N5/aʼ ; ! "pájaro, bird"
kʼel:kʼel N5/aʼ ; ! ""
kʼoy:kʼoy N5/iʼ ; ! ""
akʼwal:akʼwal N5/aʼ ; ! "niño, child"
alaʼ:alaʼ N5/bʼoniʼ ; ! ""
xtän:xtän N5/iʼ ; ! "señorita, girl"
achin:achin N5/aʼ ; ! ""
ixöq:ixöq N5/iʼ ; ! "mujer, woman"
winäq:winäq N5/iʼ ; ! "persona (ser humano), person (human being)"
chiköp:chiköp N5/iʼ ; ! ""
xikʼ:xikʼ N5/aʼ ; ! ""
masat:masat N5/iʼ ; ! ""
chʼumil:chʼumil N5/aʼ ; ! ""
kʼoj:kʼoj N5/aʼ ; ! ""

abʼäj:abʼäj SUSTANTIVO ; ! "piedra, stone"
achamaqʼiʼ:achamaqʼiʼ SUSTANTIVO ; ! "indígena, indigenous"
ajpatän:ajpatän SUSTANTIVO ; ! "cofradía, religious society"
äm:äm SUSTANTIVO ; ! "araña, spider"
aqaj:aqaj SUSTANTIVO ; ! "panal, honeycomb"
aqʼomanel:aqʼomanel SUSTANTIVO ; ! "curandero, shaman, medicine man"
atzʼam:atzʼam SUSTANTIVO ; ! "sal, salt"
bʼanobʼäl:bʼanobʼäl SUSTANTIVO ; ! "costumbre, religious custom"
bʼaq:bʼaq SUSTANTIVO ; ! "hueso, bone"
bʼixanel:bʼixanel SUSTANTIVO ; ! "cantor, singer"
chʼabʼäl:chʼabʼäl SUSTANTIVO ; ! "idioma, language"
chʼabʼ:chʼabʼ SUSTANTIVO ; ! "flecha, arrow"
chaj:chaj SUSTANTIVO ; ! "cenizas, ashes"
cheʼ:cheʼ SUSTANTIVO ; ! "árbol, tree"
chinchiʼy:chinchiʼy SUSTANTIVO ; ! "sonaja, chinchín, rattle"
etamanel:etamanel SUSTANTIVO ; ! "sabio, wiseman"
ibʼoy:ibʼoy SUSTANTIVO ; ! "armadillo, armadillo"
ijaʼtz:ijaʼtz SUSTANTIVO ; ! "semilla, seed"
ixkʼäq:ixkʼäq SUSTANTIVO ; ! "uña, fingernail"
jikonel:jikonel SUSTANTIVO ; ! "sobadora, fertility massager"
jiqʼobʼäl:jiqʼobʼäl SUSTANTIVO ; ! "fruta, fruit"
jomeʼet:jomeʼet SUSTANTIVO ; ! "corteza, bark"
kaʼaj:kaʼaj SUSTANTIVO ; ! "rancho, ranch"
kär:kär SUSTANTIVO ; ! "pescado, fish"
kaxlan:kaxlan SUSTANTIVO ; ! "extranjero, forastero, foreigner, strange"
kʼaybʼäl:kʼaybʼäl SUSTANTIVO ; ! "mercado, market"
kʼechelaj:kʼechelaj SUSTANTIVO ; ! "bosque, forest"
kʼoj:kʼoj SUSTANTIVO ; ! "máscara, mask"
kʼolaj:kʼolaj SUSTANTIVO ; ! "pelota, bola, ball"
lemaʼ:lemaʼ SUSTANTIVO ; ! "cuento, story"
mes:mes SUSTANTIVO ; ! "gato, cat"
molaj:molaj SUSTANTIVO ; ! "grupo-, group -"
moloj:moloj SUSTANTIVO ; ! "junta, reunión, meeting"
muqbʼäl:muqbʼäl SUSTANTIVO ; ! "cementerio, camposanto, cemetery"
nimaʼaj:nimaʼaj SUSTANTIVO ; ! "bambú, bamboo"
nimaxik:nimaxik SUSTANTIVO ; ! "halcón, falcon"
niʼx:niʼx SUSTANTIVO ; ! "insecto, insect"
noxtiʼ:noxtiʼ SUSTANTIVO ; ! "cacto, cactus"
palamax:palamax SUSTANTIVO ; ! "mariposa, butterfly"
piril:piril SUSTANTIVO ; ! "pito, flute"
ponibʼäl:ponibʼäl SUSTANTIVO ; ! "incensario, incense burner"
punpün:punpün SUSTANTIVO ; ! "ganso, oca, goose"
qʼanchiʼ:qʼanchiʼ SUSTANTIVO ; ! "gringo, gringo"
qʼaqʼ:qʼaqʼ SUSTANTIVO ; ! "fuego, fire"
qʼayïs:qʼayïs SUSTANTIVO ; ! "pasto, grass"
qʼij:qʼij SUSTANTIVO ; ! "día, day"
qʼojomanelaʼ:qʼojomanelaʼ SUSTANTIVO ; ! "marimberos, marimba players"
qʼojomanel:qʼojomanel SUSTANTIVO ; ! "músico, musician"
qʼojom:qʼojom SUSTANTIVO ; ! "música, music"
qʼolonel:qʼolonel SUSTANTIVO ; ! "payaso, clown"
ruwachʼulew:ruwachʼulew SUSTANTIVO ; ! "mundo, world"
sanayiʼ:sanayiʼ SUSTANTIVO ; ! "arena, sand"
sanïk:sanïk SUSTANTIVO ; ! "hormiga, ant"
saqmoloʼ:saqmoloʼ SUSTANTIVO ; ! "huevo, egg"
saseʼ:saseʼ SUSTANTIVO ; ! "hígado, liver"
selibʼäl:selibʼäl SUSTANTIVO ; ! "cuchillo, knife"
tijonel:tijonel SUSTANTIVO ; ! "maestro, teacher"
tinamït:tinamït SUSTANTIVO ; ! "pueblo, town"
toj:toj SUSTANTIVO ; ! "ofrenda, offering"
tonkʼor:tonkʼor SUSTANTIVO ; ! "tambor (m), drum"
tukur:tukur SUSTANTIVO ; ! "búho, owl"
tzinïn:tzinïn SUSTANTIVO ; ! "cascabel (m), shaker"
tzʼiteʼ:tzʼiteʼ SUSTANTIVO ; ! "palo pito, red beans"
ükʼ:ükʼ SUSTANTIVO ; ! "piojo, louse"
warabʼäl:warabʼäl SUSTANTIVO ; ! "posada, hospedaje, room"
warabʼenïk:warabʼenïk SUSTANTIVO ; ! "velorio, wake"
winaqiʼ:winaqiʼ SUSTANTIVO ; ! "personas, people"
xeʼel:xeʼel SUSTANTIVO ; ! "raíz, root"
xiʼl:xiʼl SUSTANTIVO ; ! "grillo, cricket"
xukulbʼäl:xukulbʼäl SUSTANTIVO ; ! "altar, altar"
yaʼ:yaʼ SUSTANTIVO ; ! "agua, water"

!ruqʼaʼ tinamït:ruqʼaʼ tinamït SUSTANTIVO-DE-SUSTANTIVO ; ! "aldea, village"
!
!aqʼaʼ:aqʼaʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "noche, night"
!choy:choy SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "lago, lake"
!ikʼ:ikʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "luna, moon"
!jut:jut SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "gusano, worm"
!kaj:kaj SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "cielo, sky"
!kaqʼïqʼ:kaqʼïqʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "viento, wind"
!kumätz:kumätz SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "serpiente, snake"
!palow:palow SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "mar, sea"
!qʼij:qʼij SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "sol, sun"
!raqän yaʼ:raqän yaʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "río, river"
!sïbʼ:sïbʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "humo, smoke"
!sutzʼ:sutzʼ SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "nube, cloud"
!xanän:xanän SUSTANTIVO-GENERALMENTE-NO-POSEIDO ; ! "zancudo, mosquito"
!
!aqanaj:aqanaj SUSTANTIVO-INALIENABLE ; ! "pie, foot"
!atitaj:atitaj SUSTANTIVO-INALIENABLE ; ! "abuela materna, motherʼs mother"
!atitaj:atitaj SUSTANTIVO-INALIENABLE ; ! "abuela paterna, fatherʼs mother"
!chʼekaj:chʼekaj SUSTANTIVO-INALIENABLE ; ! "rodilla, knee"
!eyaj:eyaj SUSTANTIVO-INALIENABLE ; ! "diente, tooth"
!ijaj:ijaj SUSTANTIVO-INALIENABLE ; ! "espalda, back"
!mamaʼaj:mamaʼaj SUSTANTIVO-INALIENABLE ; ! "abuelo paterno, fatherʼs father"
!qʼabʼaj:qʼabʼaj SUSTANTIVO-INALIENABLE ; ! "mano, hand"
!qulaj:qulaj SUSTANTIVO-INALIENABLE ; ! "cuello, neck"
!tataʼaj:tataʼaj SUSTANTIVO-INALIENABLE ; ! "padre, father"
!teʼej:teʼej SUSTANTIVO-INALIENABLE ; ! "madre, mother"
!tiʼij:tiʼij SUSTANTIVO-INALIENABLE ; ! "carne, meat"
!tzamaj:tzamaj SUSTANTIVO-INALIENABLE ; ! "nariz, nose"
!tzʼumaj:tzʼumaj SUSTANTIVO-INALIENABLE ; ! "seno, breast"
!wachaj:wachaj SUSTANTIVO-INALIENABLE ; ! "ojo, eye"
!wiʼaj:wiʼaj SUSTANTIVO-INALIENABLE ; ! "cabello, hair"
!xikinaj:xikinaj SUSTANTIVO-INALIENABLE ; ! "oído, ear"
!
!ronojel:ronojel SUSTANTIVO-RELACIONAL ; ! "todos, all"
!
!rujey:rujey SUSTANTIVO-SIEMPRE-POSEIDO ; ! "cola, tail"
!ruxaq:ruxaq SUSTANTIVO-SIEMPRE-POSEIDO ; ! "hoja, leaf"
!
!jay:jay SUSTANTIVO-SUPLETIVO ; ! "casa, house/shelter"
!
!kamïk:kamïk SUSTANTIVO-VERBAL ; ! "morir, die"

tzij:tzij N1 ; ! "palabra"
kʼaslen:kʼaslen N1 ; ! "palabra"
kʼaslen:kʼaslem N1 ; ! "palabra"
kʼaslemal:kʼaslemal N1 ; ! "palabra"

LEXICON Propios

Luʼ:Luʼ NP-ANT ; ! ""

LEXICON Adjetivos

ütz:ütz A ; ! "bueno, good"
al:al A ; ! "pesado (que pesa), heavy"
chäq’:chäq’ A ; ! "maduro, ripe"
k’ak’a’:k’ak’a’ A ; ! "nuevo, new"
käq:käq A ; ! "rojo, red"
k’atän:k’atän A ; ! "caliente, hot"
ki’:ki’ A ; ! "ser delicioso, be tasty"
k’ïy:k’ïy A ; ! "muchos, many"
ko’öl:ko’öl A ; ! "pequeño, small"
nïm:nïm A ; ! "grande, big"
nojnäq:nojnäq A ; ! "lleno, full"
q’än:q’än A ; ! "amarillo, yellow"
q’eq:q’eq A ; ! "negro, black"
räx:räx A ; ! "verde, green"
ri’j:ri’j A ; ! "viejo, old"
säq:säq A ; ! "blanco, white"
setesïk:setesïk A ; ! "redondo / circular, round"

LEXICON Adverbios

kʼa:kʼa ADV ; ! "bien"
kan:kan ADV ; ! "muy"
chuqaʼ:chuqaʼ ADV ; ! "también" 
chuwaʼq:chuwaʼq ADV ; ! "mañana"
iwir:iwir ADV ; ! "ayer"

wakami:wakami ADV ; ! ""
chaqʼaʼ:chaqʼaʼ ADV ; ! ""
jantapeʼ:jantapeʼ ADV ; ! ""
ojer:ojer ADV ; ! ""
myer:myer ADV ; ! ""
tiqaqʼij:tiqaqʼij ADV ; ! ""
paqʼij:paqʼij ADV ; ! ""
kabʼij:kabʼij ADV ; ! ""
kabʼijir:kabʼijir ADV ; ! ""
junabʼir:junabʼir ADV ; ! ""

näj:näj ADV ; ! ""
waweʼ:waweʼ ADV ; ! ""

chanim:chanim ADV ; ! ""
ütz:ütz ADV ; ! ""
eqal:eqal ADV ; ! ""
jutaqil:jutaqil ADV ; ! ""
aninäq:aninäq ADV ; ! ""

qitzij:qitzij ADV ; ! ""


LEXICON Intr

oyon:oyon IV ; ! "llamar"
atin:atin IV ; ! "bañarse"
tzʼuyeʼ:tzʼuyeʼ IV ; ! "sentarse"
el:el IV ; ! "salir"
wär:wär IV ; ! "dormir"
waʼ:waʼ IV ; ! "comer"
samäj:samäj IV ; ! "trabajar"
oqʼ:oqʼ IV ; ! "llorar"
bʼe:bʼe IV ; ! "ir"
kʼaseʼ:kʼaseʼ IV ; ! "vivir"

LEXICON Tran

tzʼu:tzʼu TV ; ! "see, look at"
tzʼët:tzʼët TV ; ! "mirar, look at"
bʼän:bʼän TV ; ! "hacer, do"
naʼ:naʼ TV ; ! "sentir"
löqʼ:löqʼ TV ; ! "comprar"
tïk:tïk TV ; ! "sembrar"
chäp:chäp TV ; ! "agarrar"

LEXICON Punctuación

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ; 
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ; 
%¿%<lquest%>:%¿ # ; 
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
（%<lpar%>:（ # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
）%<rpar%>:） # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;
…%<sent%>:… # ;

